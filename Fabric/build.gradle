buildscript {
	repositories {
		maven {
			url = "https://maven.fabricmc.net/"
			content {
				includeGroup "net.fabricmc"
				includeGroup "fabric-loom"
			}
		}
		mavenCentral()
	}
	dependencies {
		classpath "fabric-loom:fabric-loom.gradle.plugin:1.1.14"
	}
}

evaluationDependsOn(":Xplat") //pls

apply plugin: "java"
apply plugin: "fabric-loom"

archivesBaseName = "${rootProject.name}-fabric-1.20.1"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}
java.withSourcesJar()

repositories {
//	maven {
//		url "https://maven.vram.io/"
//		content { includeGroup("io.vram") }
//	}
	maven {
		url "https://maven.terraformersmc.com/"
		content {
//			includeGroup("com.terraformersmc") //Grondaaaaag, canvas transitively deps on modmenu
			includeGroup "dev.emi"
		}
	}
//	maven {
//		url "https://api.modrinth.com/maven"
//		content { includeGroup "maven.modrinth" }
//	}
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:1.20.1"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:0.14.21"
	modImplementation "net.fabricmc.fabric-api:fabric-api:0.85.0+1.20.1"
	compileOnly "org.jetbrains:annotations:24.0.1"
	
	implementation project(":Xplat") //funny xplat
	
	def runWithCanvas = false
	def runWithSodium = false
	def runWithEmi = true
	
	if(runWithCanvas) {
		//modImplementation "io.vram:frex-fabric-mc119:8.0.327"
		//modLocalRuntime "io.vram:canvas-fabric-mc119:3.0.2547"
	} else {
		//modCompileOnly "io.vram:frex-fabric-mc119:8.0.327" //TODO, canvas not updated to 1.20
	}
	
	if(runWithSodium) {
		//TODO: it's broken for some reason and doesn't load the joml jar in jar
		//modLocalRuntime "maven.modrinth:sodium:mc1.18.2-0.4.1"
	}

	modCompileOnly "dev.emi:emi-fabric:1.0.11+1.20.1:api"
	if(runWithEmi) {
		modLocalRuntime "dev.emi:emi-fabric:1.0.11+1.20.1"
	}
}

processResources {
	from project(":Xplat").sourceSets.main.resources
	
	inputs.property "version", project.version
	filesMatching("**/fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	source(project(":Xplat").sourceSets.main.allSource)
}

//loom doesn't generate run configs by default in subprojects
loom {
	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("run")
		}
	}
	
	//nor can it figure out the refmap name in this situation
	mixin {
		defaultRefmapName = "${project.modName}.refmap.json"
	}
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}